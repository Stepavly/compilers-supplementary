-- Expression evaluator

import List;
import State;


-- The evaluator itself: takes a state and an expression,
-- returns integer value
--
-- An expression is represented by a data structure of the following shape:
--
-- expr = Var   (string)             |
--        Const (int)                |
--        Binop (string, expr, expr)

public fun evalBinop (left, right, bop) {
  case bop of
    "+" -> left + right
  | "-" -> left - right
  | "*" -> left * right
  | "/" -> left / right
  | "%" -> left % right
  | "!!" -> left !! right
  | "&&" -> left && right
  | "==" -> left == right
  | "!=" -> left != right
  | "<=" -> left <= right
  | "<" -> left < right
  | ">=" -> left >= right
  | ">" -> left > right
  esac
}

public fun evalExpr (st, expr) {
  case expr of 
    Var (varName) -> st(varName)
  | Const (n) -> n
  | Binop (bop, left, right) -> evalBinop(evalExpr(st, left), evalExpr(st, right), bop)
  esac
}
