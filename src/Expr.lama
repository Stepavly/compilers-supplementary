-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (bop, left, right) {
  case bop of
    "+" -> left + right
  | "-" -> left - right
  | "*" -> left * right
  | "/" -> left / right
  | "%" -> left % right
  | "!!" -> left !! right
  | "&&" -> left && right
  | "==" -> left == right
  | "!=" -> left != right
  | "<=" -> left <= right
  | "<" -> left < right
  | ">=" -> left >= right
  | ">" -> left > right
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun addDef (state, def) {
  -- printf ("Adding def " ++ def.string ++ "\n");
  case def of
    Var (varNames) -> addNames (state, varNames)
  | Fun (name, args, body) -> addFunction (state, name, args, body)
  esac
}

fun matchArg (state, [argName, argValue]) {
  addName (state, argName, Var (argValue))
}

(* Assignment *)
fun eval (conf@[state, world], expr) {
  -- printf(expr.string ++ "\n");
  case expr of
    Skip -> [conf, Nothing]
  | Var (varName) -> 
      case lookup (state, varName) of
        Var (value) -> [conf, value]
      esac
  | Const (n) -> [conf, n]
  | Ref (r) -> [conf, r]
  | Read (varExpr) -> 
      case eval (conf, varExpr) of
        [[state, world], varName] ->
          case readWorld(world) of
            [value, newWorld] -> [[state <- [varName, Var (value)], newWorld], Nothing]
          esac
      esac
  | Write (expr) -> 
      case eval (conf, expr) of
        [[state, world], value] -> [[state, writeWorld (value, world)], Nothing]
      esac
  | Binop (bop, left, right) -> 
      case eval (conf, left) of
        [conf, leftValue] ->
          case eval (conf, right) of
            [conf, rightValue] -> 
                [conf, evalOp (bop, leftValue, rightValue)]
          esac
      esac
  | Ignore (expr) -> 
      case eval(conf, expr) of
        [conf, _] -> [conf, Nothing]
      esac
  | Assn (targetNode, valueNode) -> 
      case eval (conf, targetNode) of
        [conf, varName] ->
            case eval (conf, valueNode) of
              [[state, world], value] -> [[state <- [varName, Var (value)], world], value]
            esac
      esac
  | Seq (head, tail) ->
      case eval (conf, head) of
        [conf, _] -> eval (conf, tail)
      esac
  | If (condition, ifBody, elseBody) ->
    case eval (conf, condition) of
      [conf, 0] -> eval (conf, elseBody)
    | [conf, n] -> eval (conf, ifBody)
    esac
  | node@While (condition, body) ->
    case eval (conf, condition) of
      [conf, 0] -> [conf, Nothing]
    | [conf, _] -> 
        case eval (conf, body) of
          [conf, _] -> eval (conf, node)
        esac
    esac
  | DoWhile (body, condition) -> eval (conf, Seq (body, While (condition, body)))
  | Scope (defs, expr) ->
      -- printf ("Adding defs " ++ defs.string ++ "\n");
      case foldr (addDef, enterScope (state), defs) of
        state ->
          -- printf("Scope added\n");
          case eval ([state, world], expr) of
            [[state, world], n] -> [[leaveScope (state), world], n]
          esac
      esac
  | Call (func, argsNode) ->
      case lookup (state, func) of
        Fun (args, body) -> 
          case evalList (conf, argsNode) of
            [conf@[state, world], argsValues] ->
              case enterFunction (state) of
                funcState -> 
                  case foldl (matchArg, funcState, zip (args, argsValues)) of
                    funcState ->
                      case eval([funcState, world], body) of
                        [[funcState, world], n] -> [[leaveFunction (state, getGlobal (funcState)), world], n]
                      esac
                  esac
              esac
          esac
      esac
  | other -> failure (other.string)
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}